! time ../../madx64 testmatchptc.madx > matchptc.out 2>&1

!---|----1----|----2----|----3----|----4----|----5----|----6----|----7----|---8;
Option,  -warn,info;

Title, "Matching an insertion of the LHC 6.5 lattice with MADX";

!---------------------------------------------------------------;
!           CALL IN SELECTION, SEQUENCE and OPTICS FILES;
!---------------------------------------------------------------;
option, -echo;
call, file = "input/V6.5.seq";
call, file = "input/V6.5.coll.str";
option, echo, warn;

Beam, particle = proton, sequence=lhcb1, energy = 7000.0;
Beam, particle = proton, sequence=lhcb2, energy = 7000.0;
! the 'bv' parameter indicates that beam2 rotates counter-clockwise

! select IR8 of beam1:
call,file="toolkit/select.ir8.b1.madx";
call,file="toolkit/twissini.ir8.b1.madx";

! Create table for storing data
n=0;
create,table=str.ip8.b1,column=n,kq4.l8b1, kq5.l8b1, kq6.l8b1, kq7.l8b1, kq8.l8b1, kq9.l8b1, kq10.l8b1, kqtl11.l8b1, kqt12.l8b1, kqt13.l8b1,kq4.r8b1, kq5.r8b1, kq6.r8b1, kq7.r8b1, kq8.r8b1, kq9.r8b1, kq10.r8b1, kqtl11.r8b1, kqt12.r8b1, kqt13.r8b1,beta.ip8,tar;

! fill the table
fill,table=str.ip8.b1;

! set match variables
muxip8 := 3.204; ! Horizontal phase advance over IR8
muyip8 := 2.804; ! Vertical phase advance over IR8
scale := 23348.89927;
qtlimit1 := 1.0*220.0/scale;
qtlimit2 := 1.0*200.0/scale;
qtlimit3 := 1.0*130.0/scale;
qtlimit4 := 1.0*160.0/scale;

show, bir8b1;
show, eir8b1;

tar=0;
ncall=0;
while( n < 2 ) {  ! tar < 1E-10 ) {
n=n+1;
beta.ip8=beta.ip8*.6;
match, use_macro;
vary,name=KQ4.L8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ5.L8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ6.L8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ7.L8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ8.L8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ9.L8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ10.L8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQTL11.L8B1,step=1.0e-6,LOWER=-qtlimit3,UPPER=qtlimit3;
vary,name=KQT12.L8B1,step=1.0e-6,LOWER=-qtlimit3,UPPER=qtlimit3;
vary,name=KQT13.L8B1,step=1.0e-6,LOWER=-qtlimit3,UPPER=qtlimit3;

vary,name=KQ4.R8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ5.R8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ6.R8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ7.R8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ8.R8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ9.R8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQ10.R8B1,step=1.0e-6,LOWER=-qtlimit2,UPPER=qtlimit2;
vary,name=KQTL11.R8B1,step=1.0e-6,LOWER=-qtlimit3,UPPER=qtlimit3;
vary,name=KQT12.R8B1,step=1.0e-6,LOWER=-qtlimit3,UPPER=qtlimit3;
vary,name=KQT13.R8B1,step=1.0e-6,LOWER=-qtlimit3,UPPER=qtlimit3;

m1: macro = {
  ncall=ncall+1;
  ptc_create_universe ;
  ptc_create_layout, model=2, method=4, nst=1, time=true, exact=true ;
  ptc_setswitch, fringe=true, time=true, totalpath=false ;
  ptc_twiss, closed_orbit=true, no=1, icase=5, beta0=bir8b1 ;
  ptc_end ;
}

constraint, expr = table(ptc_twiss,ip8,betx) = beta.ip8;
constraint, expr = table(ptc_twiss,ip8,bety) = beta.ip8;
constraint, expr = table(ptc_twiss,ip8,alfx) = 0;
constraint, expr = table(ptc_twiss,ip8,alfy) = 0;
constraint, expr = table(ptc_twiss,ip8,dx  ) = 0;
constraint, expr = table(ptc_twiss,ip8,dpx ) = 0;

constraint, expr = table(ptc_twiss,e.ds.r8.b1,alfx) = eir8b1->alfx;
constraint, expr = table(ptc_twiss,e.ds.r8.b1,alfy) = eir8b1->alfy;
constraint, expr = table(ptc_twiss,e.ds.r8.b1,betx) = eir8b1->betx;
constraint, expr = table(ptc_twiss,e.ds.r8.b1,bety) = eir8b1->bety;
constraint, expr = table(ptc_twiss,e.ds.r8.b1,dx  ) = eir8b1->dx ;
constraint, expr = table(ptc_twiss,e.ds.r8.b1,dpx ) = eir8b1->dpx;
constraint, expr = table(ptc_twiss,e.ds.r8.b1,mux ) = muxip8;
constraint, expr = table(ptc_twiss,e.ds.r8.b1,muy ) = muyip8;

jacobian,calls=30,tolerance=1.e-24;
endmatch;

fill,table=str.ip8.b1;
};
write,table=str.ip8.b1,file="output/str.ip8.b1.ptc.dat";
value, ncall;
stop;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
MAD-X PTC results: real  0m43.754s
ncall              =                347 ;
Final Penalty Function =   1.91197323e+01
 ++++++++++ JACOBIAN ended: minimum found

Variable                 Final Value  Initial Value Lower Limit  Upper Limit
--------------------------------------------------------------------------------
kq4.l8b1                 -3.96216e-03 -4.13755e-03 -8.56571e-03  8.56571e-03
kq5.l8b1                  4.71024e-03  4.98994e-03 -8.56571e-03  8.56571e-03
kq6.l8b1                 -5.56054e-03 -5.32911e-03 -8.56571e-03  8.56571e-03
kq7.l8b1                  8.42626e-03  8.28112e-03 -8.56571e-03  8.56571e-03
kq8.l8b1                 -5.63937e-03 -5.73919e-03 -8.56571e-03  8.56571e-03
kq9.l8b1                  7.46952e-03  7.25647e-03 -8.56571e-03  8.56571e-03
kq10.l8b1                -6.23247e-03 -6.34449e-03 -8.56571e-03  8.56571e-03
kqtl11.l8b1               6.00613e-04  6.55701e-04 -5.56771e-03  5.56771e-03
kqt12.l8b1               -3.08883e-03 -3.08538e-03 -5.56771e-03  5.56771e-03
kqt13.l8b1                3.81700e-03  3.79994e-03 -5.56771e-03  5.56771e-03
kq4.r8b1                  4.03951e-03  4.23195e-03 -8.56571e-03  8.56571e-03
kq5.r8b1                 -3.66356e-03 -3.92060e-03 -8.56571e-03  8.56571e-03
kq6.r8b1                  4.29931e-03  4.16994e-03 -8.56571e-03  8.56571e-03
kq7.r8b1                 -6.98133e-03 -6.81314e-03 -8.56571e-03  8.56571e-03
kq8.r8b1                  6.64499e-03  6.77525e-03 -8.56571e-03  8.56571e-03
kq9.r8b1                 -7.33852e-03 -7.48439e-03 -8.56571e-03  8.56571e-03
kq10.r8b1                 7.92704e-03  7.75841e-03 -8.56571e-03  8.56571e-03
kqtl11.r8b1              -4.73818e-03 -4.75306e-03 -5.56771e-03  5.56771e-03
kqt12.r8b1               -1.76696e-03 -1.75630e-03 -5.56771e-03  5.56771e-03
kqt13.r8b1               -3.63055e-03 -3.62384e-03 -5.56771e-03  5.56771e-03

