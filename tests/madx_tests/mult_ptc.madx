option, -echo, -info;
TITLE, 'Test of PTC MULT';

seq: sequence, refer = entry, l = 1;
mlt  : multipole , knl = { 0.05, knl1, 0.07, 0.09},  ksl= {0.01, -0.03, ksl2} , at=0;
ENDSEQUENCE ;

BEAM, PARTICLE=PROTON, ENERGY=450 ;
use, sequence = seq ;

i=-1;
while (i<1.0) {

knl1=i     ;
ksl2=i*0.1 ;

!PTC
ptc_create_universe;
//model=DKD, method=Yoshida6
ptc_create_layout model=1, method=6, nst=5, exact=true, time=true, closed_layout=false ;
ptc_setswitch debuglevel=1, nocavity=true, time=true, totalpath=false, fringe=false;

ptc_start x=-1e-3, px=3e-3, y=7e-5, py=-3e-4, t=1e-3, pt=0;
ptc_observe place = mlt ;
ptc_trackline, turns=1, onetable=true, file='mult.tfs';

ptc_track_end;
ptc_end;

x  =table(trackone, x ,2) ;
px =table(trackone, px,2) ;
y  =table(trackone, y, 2) ;
py =table(trackone, py,2) ;
t  =table(trackone, t, 2) ;
pt =table(trackone, pt,2) ;
s  =table(trackone, s, 2) ;

PRINTF,TEXT="PTC_MULT  %.2g %.2g %18.12g  %18.12g  %18.12g  %18.12g  %18.12g  %18.12g  ", VALUE = s, knl1, x, px, y, py, t, pt;
i = i+0.1;

};

stop ;
