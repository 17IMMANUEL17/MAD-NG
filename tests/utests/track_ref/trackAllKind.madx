option, -info, -echo, -rbarc ;

/* Run and data extraction assuming nodump=0 below:
cd track_ref
../madx64 trackAllKind.madx > trackAllKindPtc.out 2>&1 ; tail -n 5 trackAllKindPtc.out
grep "^@@\|%%" trackAllKindPtc.out | grep -B 1 "^%%" | grep -v "\-\-" > trackAllKindPtc_res.txt
grep "^@@\|%%" trackAllKindPtc.out > trackAllKindPtc_dump.txt
grep "warn"    trackAllKindPtc.out > trackAllKindPtc_warn.txt
cd ..
*/

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! setup
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

call, file="trackAllKind_macro.madx" ;
set, format=" -.16e" ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! prepare tables: Cptc, Cptc_cfg and Cptc_tmp
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

exec, prtp(Cptc) ;

! fill Cptc_cfg:
! "cfgid",
! "energy","model","method","nst","no","icase","debug",
! "x","px","y","py","t","pt","betx","bety","betz"

no=2; icase=56; debug=2; nodump=1;
x=3e-3; px=-2e-4; y=-2e-3; py=3e-4; t=1e-5; pt=2e-5; betx=1; bety=1; betz=1;

cfgid  = 1;
energy = 1;  while (energy <= 6500) {
model  = 1;  while (model  <= 2   ) {
method = 2;  while (method <= 6   ) {
nst    = 1;  while (nst    <= 3   ) {

  fill, table = Cptc_cfg ; cfgid = cfgid + 1;

nst    = nst    + 1;    }
method = method + 2;    }
model  = model  + 1;    }
energy = energy + 6499; }

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! run the tests from Cptc_cfg, save the results in Cptc.txt
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

seqDRIFT: sequence, l=5 ;
  DRIFT1: drift, at=0.75, l=1.5 ;
  DRIFT2: drift, at=3.25, l=1.5 ;
endsequence ;

exec, rchk(Cptc, DRIFT) ;

! ------

seqKICK: sequence, l=5 ;
  KICK1: kicker, at=0.75, l=1.5, hkick= 0.25e-3, vkick=-0.25e-3 ;
  KICK2: kicker, at=3.25, l=1.5, hkick=-0.25e-3, vkick= 0.25e-3 ;
endsequence ;

exec, rchk(Cptc, KICK) ;

! ------

seqMULT: sequence, l=5 ;
  MULT1: multipole, at=0.75, lrad=1.5, knl={ 0.05*1.5, 0.25*1.5}, angle= 0.05*1.5 ;
  MULT2: multipole, at=3.25, lrad=1.5, knl={-0.05*1.5,-0.25*1.5}, angle=-0.05*1.5 ;
endsequence ;

exec, rchk(Cptc, MULT) ;

! ------

seqSBEND: sequence, l=5 ;
  SBEND1: sbend, at=0.75, l=1.5, k0= 0.05, angle= 0.05*1.5, kill_ent_fringe, kill_exi_fringe ;
  SBEND2: sbend, at=3.25, l=1.5, k0=-0.05, angle=-0.05*1.5, kill_ent_fringe, kill_exi_fringe ;
endsequence ;

exec, rchk(Cptc, SBEND) ;

! ------

seqSBENDfr: sequence, l=5 ;
  SBENDfr1: sbend, at=0.75, l=1.5, k0= 0.05, angle= 0.05*1.5 ;
  SBENDfr2: sbend, at=3.25, l=1.5, k0=-0.05, angle=-0.05*1.5 ;
endsequence ;

exec, rchk(Cptc, SBENDfr) ; ! fringe

! ------

seqSBENDfh: sequence, l=5 ;
  SBENDfh1: sbend, at=0.75, l=1.5, k0= 0.05, angle= 0.05*1.5, e1= 0.05*1.5/2, e2= 0.05*1.5/1.5, hgap=0.03, fint=0.5 ;
  SBENDfh2: sbend, at=3.25, l=1.5, k0=-0.05, angle=-0.05*1.5, e1=-0.05*1.5/2, e2=-0.05*1.5/1.5, hgap=0.03, fint=0.5 ;
endsequence ;

exec, rchk(Cptc, SBENDfh) ; ! fringe, fint, hgap

! ------

seqRBEND: sequence, l=5 ;
  RBEND1: rbend, at=0.75, l=1.5, k0= 0.05, angle= 0.05*1.5, kill_ent_fringe, kill_exi_fringe ;
  RBEND2: rbend, at=3.25, l=1.5, k0=-0.05, angle=-0.05*1.5, kill_ent_fringe, kill_exi_fringe ;
endsequence ;

exec, rchk(Cptc, RBEND) ;

! ------

seqRBENDfr: sequence, l=5 ;
  RBENDfr1: rbend, at=0.75, l=1.5, k0= 0.05, angle= 0.05*1.5 ;
  RBENDfr2: rbend, at=3.25, l=1.5, k0=-0.05, angle=-0.05*1.5 ;
endsequence ;

exec, rchk(Cptc, RBENDfr) ; ! fringe

! ------

seqQUAD: sequence, l=5 ;
  QUAD1: quadrupole, at=0.75, l=1.5, k1= 0.25 ;
  QUAD2: quadrupole, at=3.25, l=1.5, k1=-0.25 ;
endsequence ;

exec, rchk(Cptc, QUAD) ;

! ------

seqSEXT: sequence, l=5 ;
  SEXT1: sextupole, at=0.75, l=1.5, k2= 0.25 ;
  SEXT2: sextupole, at=3.25, l=1.5, k2=-0.25 ;
endsequence ;

exec, rchk(Cptc, SEXT) ;

! ------

seqOCTU: sequence, l=5 ;
  OCTU1: octupole, at=0.75, l=1.5, k3= 0.25 ;
  OCTU2: octupole, at=3.25, l=1.5, k3=-0.25 ;
endsequence ;

exec, rchk(Cptc, OCTU) ;

! ------

seqDECA: sequence, l=5 ;
  DECA1: multipole, at=0.75, lrad=1.5, knl={0,0,0,0, 0.25*1.5} ;
  DECA2: multipole, at=3.25, lrad=1.5, knl={0,0,0,0,-0.25*1.5} ;
endsequence ;

exec, rchk(Cptc, DECA) ;

! ------

seqDODECA: sequence, l=5 ;
  DODECA1: multipole, at=0.75, lrad=1.5, knl={0,0,0,0,0, 0.25*1.5} ;
  DODECA2: multipole, at=3.25, lrad=1.5, knl={0,0,0,0,0,-0.25*1.5} ;
endsequence ;

exec, rchk(Cptc, DODECA) ;

! ------

seqSOL: sequence, l=5 ;
  SOL1: solenoid, at=0.75, l=1.5, ks= 0.25 ;
  SOL2: solenoid, at=3.25, l=1.5, ks=-0.25 ;
endsequence ;

exec, rchk(Cptc, SOL) ;

! ------

seqCAV: sequence, l=5 ;
  CAV1: rfcavity, at=0.75, l=1.5, volt=8, freq=400, lag=0.5 ;
  CAV2: rfcavity, at=3.75, l=1.5, volt=8, freq=400, lag=0.5 ;
endsequence ;

exec, rchk(Cptc, CAV) ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! save results and setup
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

write, table=Cptc, file="Cptc.txt" ;
set, format=" -8.4g" ;
write, table=Cptc_cfg, file="Cptc_cfg.txt" ;

stop;