! cd track_ref ; ../madx64 trackSBENDfr2.madx > trackSBENDfr2.out 2>&1 ; cat trackSBENDfr2.out ; cd -
! cd track_ref ; ../madx64 trackSBENDfr2.madx  | grep -A 1 'model=' ; cd -

option, -info, -echo, -rbarc ;

beam ; ! positron 1Gev

k0f =  2*pi/1e2/1.5 ;
k0d = -2*pi/1e2/1.5 ;
value, k0f, k0d ;

! SEQUENCE WITH SBENDS
seq: sequence, l=10, refer=entry ;
m1: sbend, at=0  , l=1.5 , k0:=k0f, angle:=k0f*1.5, e1=k0f*1.5/2, e2=k0f*1.5/1.5, hgap=0.03, fint=0.5 ;
m2: sbend, at=5  , l=1.5 , k0:=k0d, angle:=k0d*1.5, e1=k0d*1.5/2, e2=k0d*1.5/1.5, hgap=0.03, fint=0.5 ;
endsequence ;

set, format="-.16e";

! MADX TRACK
use, sequence=seq ;
track, onepass, onetable, dump, file="trackSBENDfr2madx." ;
start, x=4e-4, px=2e-4, y=3e-4, py=1e-4, t=2e-5, pt=1e-5;
observe, place=#s ;
observe, place=m1 ;
observe, place=m2 ;
observe, place=#e ;
run, turns=1 ;
endtrack ;

printf, text="model=madx", value={} ;
system, "tail -n 1 trackSBENDfr2madx.one" ;

! PTC TRACK
model=1;
while (model <= 2) {
  if (model == 2) { nst = 1; } else { nst = 10; }

  method=2;
  while (method <= 6) {
    use, sequence=seq ;
    ptc_create_universe;
    ptc_create_layout, model=model, method=method, nst=nst, time=true, exact=true;
    ptc_start, x=4e-4, px=2e-4, y=3e-4, py=1e-4, t=2e-5, pt=1e-5;
    ptc_observe, place=#s ;
    ptc_observe, place=m1 ;
    ptc_observe, place=m2 ;
    ptc_observe, place=#e ;
    ptc_track, icase=56, closed_orbit=false, dump, onetable, element_by_element, file="trackSBENDfr2ptc." ;
    ptc_track_end;
    ptc_end;

    printf, text="model=%g, method=%g, nst=%g", value={model, method, nst} ;
    system, "tail -n 1 trackSBENDfr2ptc.one" ;

    method = method + 2 ;
  }
  model = model + 1 ;
}

stop;
