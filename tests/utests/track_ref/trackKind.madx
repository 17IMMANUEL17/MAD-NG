! used by trackELEM.madx

beam ; ! positron 1Gev

set, format="-.16e";

! MADX TRACK
mdxTrck(TTTT): macro = {
  use, sequence=seqTTTT ;
  track, onepass, onetable, dump, file="trackTTTTmadx." ;
  start, x=x0, px=px0, y=y0, py=py0, t=t0, pt=pt0;
  observe, place=#s ;
  observe, place=m1 ;
  observe, place=m2 ;
  observe, place=#e ;
  run, turns=1 ;
  endtrack ;

  printf, text="model=madx", value={} ;
  system, "tail -n 1 trackTTTTmadx.one" ;
}

! PTC TRACK
ptcTrck(TTTT): macro = {
  model=1;
  while (model <= 1) {
    if (model == 2) { nst = 1; } else { nst = 3; }

    method=2;
    while (method <= 6) {
      use, sequence=seqTTTT ;
      ptc_create_universe;
      ptc_create_layout, model=model, method=method, nst=nst, time=true, exact=true;
      ptc_start, x=x0, px=px0, y=y0, py=py0, t=t0, pt=pt0;
      ptc_observe, place=#s ;
      ptc_observe, place=m1 ;
      ptc_observe, place=m2 ;
      ptc_observe, place=#e ;
      ptc_track, icase=icase, closed_orbit=false, dump, onetable, element_by_element, file="trackTTTTptc." ;
      ptc_track_end;
      ptc_end;

      printf, text="model=%g, method=%g, nst=%g", value={model, method, nst} ;
      system, "tail -n 1 trackTTTTptc.one" ;

      method = method + 2 ;
    }
    model = model + 1 ;
  }
}
